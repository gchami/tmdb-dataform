config {
  type: "table",
  schema: dataform.projectConfig.vars.TARGET_DATA,
  description: "TMDB Movie Cast",
  dependOnDependencyAssertions: true,
  bigquery: {
    clusterBy: ["id"]
  },
  assertions: {
    uniqueKey: ["id", "pos"],
    nonNull: ["id", "pos", "cast_id", "person_id", "export_date"]
  },
  tags: ["TRANSFORM_DATA"]
}


SELECT
    id
  , pos
  , SAFE_CAST(JSON_VALUE(movie_cast.cast_id) AS INT64) AS cast_id
  , JSON_VALUE(movie_cast.character) AS character
  , SAFE_CAST(JSON_VALUE(movie_cast.id) AS INT64) AS person_id
  , JSON_VALUE(movie_cast.name) AS name
  , JSON_VALUE(movie_cast.original_name) AS original_name
  , JSON_VALUE(movie_cast.known_for_department) AS known_for_department
  , SAFE_CAST(JSON_VALUE(movie_cast.popularity) AS FLOAT64) AS popularity
  , SAFE_CAST(JSON_VALUE(movie_cast.gender) AS INT64) AS gender
  , IF(movie_cast.profile_path IS NOT NULL, FORMAT("https://image.tmdb.org/t/p/w500%s", JSON_VALUE(movie_cast.profile_path)), NULL) AS profile_image
  , JSON_VALUE(movie_cast.credit_id) AS credit_id
  , SAFE_CAST(JSON_VALUE(movie_cast.adult) AS BOOL) AS adult
  , export_date
FROM (SELECT
          id
        , pos
        , JSON_QUERY_ARRAY(response_json.cast)[ORDINAL(pos)] AS movie_cast
        , export_date
      FROM ${ ref({ schema: dataform.projectConfig.vars.TARGET_DATA, name: "tmdb_parsed_data" }) }
        CROSS JOIN UNNEST(GENERATE_ARRAY(1, ARRAY_LENGTH(JSON_QUERY_ARRAY(response_json.cast)))) AS pos
      WHERE type = 'movie'
        AND response_type = 'credits'
        AND response_json IS NOT NULL
      QUALIFY ROW_NUMBER() OVER (PARTITION BY id
                                            , pos
                                 ORDER BY export_date DESC) = 1) AS dt
