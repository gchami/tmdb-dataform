config {
  type: "table",
  schema: dataform.projectConfig.vars.TARGET_DATA,
  description: "TMDB Movie Genres",
  dependOnDependencyAssertions: true,
  bigquery: {
    clusterBy: ["id"]
  },
  assertions: {
    uniqueKey: ["id", "pos"],
    nonNull: ["id", "pos", "genre_id", "export_date"]
  },
  tags: ["TRANSFORM_DATA"]
}


SELECT
    id
  , pos
  , SAFE_CAST(JSON_VALUE(genre.id) AS INT64) AS genre_id
  , JSON_VALUE(genre.name) AS name
  , export_date
FROM (SELECT
          id
        , pos
        , JSON_QUERY_ARRAY(response_json.genres)[ORDINAL(pos)] AS genre
        , export_date
      FROM ${ ref({ schema: dataform.projectConfig.vars.TARGET_DATA, name: "tmdb_parsed_data" }) }
        CROSS JOIN UNNEST(GENERATE_ARRAY(1, ARRAY_LENGTH(JSON_QUERY_ARRAY(response_json.genres)))) AS pos
      WHERE type = 'movie'
        AND response_type = 'detail'
        AND response_json IS NOT NULL
        AND response_json.status_code IS NULL
      QUALIFY ROW_NUMBER() OVER (PARTITION BY id
                                            , pos
                                 ORDER BY export_date DESC) = 1) AS dt
